package info.woody.api.intellij.plugin.csct.bean

import static info.woody.api.intellij.plugin.csct.util.RichTextMaker.*

/**
 * File detail info.
 *
 * @author Woody
 */
class CodeStyleCheckDetailFileData extends CodeStyleCheckSummaryFileData {
    private static final String LINE_BREAK_TAG = "<br>"
    private static final int PADDING_WIDTH = 5
    private String reportContent = ""
    String authorsKey
    List<CodeStyleCheckGlobalError> globalErrorList = new ArrayList<>()
    List<CodeStyleCheckLineError> lineErrorList = new ArrayList<>()

    /**
     * Constructor.
     *
     * @param fileName The file name.
     * @param filePath The file path.
     * @param authorsKey Unique key generated by authors.
     */
    CodeStyleCheckDetailFileData(String fileName, String filePath, String authorsKey) {
        super(fileName, filePath)
        this.authorsKey = authorsKey
    }

    /**
     * Get the error count for the current file.
     *
     * @return The error count for the current file.
     */
    int getTotalErrorCount() {
        globalErrorList.size() + lineErrorList.size()
    }

    /**
     * Generate file report.
     *
     * @return The formatted report.
     */
    String getReportContent() {
        int totalErrorCount = getTotalErrorCount()
        if (/*!reportContent &&*/ totalErrorCount > 0) {
            StringBuilder reportContentBuilder = new StringBuilder()
            reportContentBuilder.append("${newLink(filePath, filePath, this.fileName)} has ${totalErrorCount} error(s)")
            globalErrorList.each {
                String errorDescription = String.format(escapeContent(it.error), escapeArgs(it.args))
                reportContentBuilder.append(LINE_BREAK_TAG).append(newHighlight(errorDescription))
            }
            lineErrorList.each {
                String errorDescription = String.format(escapeContent(it.error), escapeArgs(it.args))
                reportContentBuilder.append(LINE_BREAK_TAG).append(String.valueOf(it.lineNumber).padRight(PADDING_WIDTH)
                        .concat(escapeContent(it.line.trim())).concat(" &lt;= ").concat(newHighlight(errorDescription)))
            }
            reportContent = reportContentBuilder.toString()
        }
        reportContent
    }
}
